buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${contractVerifierVersion}"
    }
}

apply plugin: "org.springframework.boot"
apply plugin: "spring-cloud-contract"
apply plugin: "maven-publish"

publishing {
    publications {
        stubs(MavenPublication) {
            artifactId project.name
            artifact verifierStubsJar
        }
    }
}

contracts {
    baseClassMappings {
        baseClassMapping ".*items.*", "ru.romanow.heisenbug.warehouse.web.BaseItemsControllerTest"
        baseClassMapping ".*state.*", "ru.romanow.heisenbug.warehouse.web.BaseStateControllerTest"
        baseClassMapping ".*checkout.*", "ru.romanow.heisenbug.warehouse.web.BaseCheckoutControllerTest"
        baseClassMapping ".*takeItems.*", "ru.romanow.heisenbug.warehouse.web.BaseTakeItemControllerTest"
    }
    basePackageForTests = "ru.romanow.heisenbug.warehouse.web"
    testFramework = "JUNIT5"

    contractDependency {
        stringNotation = "${project.group}:${project.name}:${project.version}"
    }

    contractRepository {
        repositoryUrl = "git://https://wGUMjtoGkLxGkFS5WcMR@gitlab.com/heisenbug-conf/heisenbug-contracts.git"
        username = "wGUMjtoGkLxGkFS5WcMR"
        password = ""
    }

    contractsMode = "REMOTE"
}

dependencies {
    testImplementation "org.codehaus.groovy:groovy-all:${groovyVersion}"
    testImplementation "org.springframework.cloud:spring-cloud-contract-spec:${springContractVersion}"
    testImplementation "org.springframework.cloud:spring-cloud-starter-contract-verifier:${springContractVersion}"
}

bootJar {
    archiveFileName = "warehouse.jar"
}